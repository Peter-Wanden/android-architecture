apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.android.architecture.blueprints.todomvvmlive"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled true
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }
    flavorDimensions "default"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    dataBinding {
        enabled = true
    }
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {
    // App's dependencies, including test
    implementation "androidx.appcompat:appcompat:$rootProject.appCompat"
    implementation "androidx.cardview:cardview:$rootProject.cardView"
    implementation "com.google.android.material:material:$rootProject.material"
    implementation "androidx.recyclerview:recyclerview:$rootProject.recyclerView"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.legacySupport"
    implementation "com.google.guava:guava:$rootProject.guavaVersion"

    // Architecture Components
    implementation "androidx.room:room-runtime:$rootProject.roomRunTime"
    annotationProcessor "androidx.room:room-compiler:$rootProject.roomCompiler"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycleExtensions"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$rootProject.lifecycleCompiler"



    // Dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"

    testImplementation "androidx.arch.core:core-testing:$rootProject.coreTesting"



    // Android Testing Support Library's runner and rules
    androidTestImplementation "androidx.test:runner:$rootProject.ext.runnerVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.ext.rulesVersion"

    androidTestImplementation "androidx.room:room-testing:$rootProject.roomTestingVersion"

    // Dependencies for Android unit tests
    androidTestImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'

    // Espresso UI Testing
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoCore"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.espressoContrib"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.espressoIntents"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$rootProject.espressoConcurrent"
    implementation "androidx.test.espresso:espresso-idling-resource:$rootProject.espressoIdlingResource"

    // Resolve conflicts between main and test APK:
    androidTestImplementation "androidx.annotation:annotation:$rootProject.annotations"
    androidTestImplementation "androidx.legacy:legacy-support-v4:$rootProject.legacySupport"
    androidTestImplementation "androidx.recyclerview:recyclerview:$rootProject.recyclerView"
    androidTestImplementation "androidx.appcompat:appcompat:$rootProject.appCompat"
    androidTestImplementation "androidx.core:core:$rootProject.coreCore"
    androidTestImplementation "com.google.android.material:material:$rootProject.material"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
}


