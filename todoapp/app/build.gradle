apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.android.architecture.blueprints.tododatabinding"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled true
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
        }

        release {
            minifyEnabled true
        }
    }
    flavorDimensions "default"

    flavorDimensions "default"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    dataBinding {
        enabled = true
    }
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {
    // App's dependencies, including test
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "com.google.android.material:material:1.1.0-alpha06"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.guava:guava:$rootProject.guavaVersion"

    // Architecture Components
    implementation "androidx.room:room-runtime:2.1.0-rc01"
    annotationProcessor "androidx.room:room-compiler:2.1.0-rc01"

    // Required?
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.2.0-alpha01"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
    // Required?
    testImplementation "androidx.arch.core:core-testing:2.1.0-beta01"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.room:room-testing:2.1.0-rc01"

    // Dependencies for Android unit tests
    androidTestImplementation  "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation  "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    androidTestImplementation  'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation  'com.google.dexmaker:dexmaker-mockito:1.2'

    // Espresso UI Testing
    androidTestImplementation  "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.2.0"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:3.2.0"
    implementation "androidx.test.espresso:espresso-idling-resource:3.2.0"

    // Resolve conflicts between main and test APK:
    androidTestImplementation "androidx.annotation:annotation:1.0.2"
    androidTestImplementation "androidx.legacy:legacy-support-v4:1.0.0"
    androidTestImplementation "androidx.recyclerview:recyclerview:1.0.0"
    androidTestImplementation "androidx.appcompat:appcompat:1.0.2"
    androidTestImplementation "androidx.core:core:1.0.2"
    androidTestImplementation "com.google.android.material:material:1.1.0-alpha06"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
}

// temp fix for: Program type already present: com.google.common.util.concurrent.ListenableFuture
configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}
